plugins {
    id 'nva.doiregistrar.service.java-library-conventions'
}

project.ext {
    jaxbTargetDir = file("$buildDir/generated/sources/xjc")
}

sourceSets.main.java.srcDirs += jaxbTargetDir

configurations {
    jaxb
}

dependencies {
    jaxb libs.bundles.xsd2java
    implementation libs.bundles.xsd2java
}


task xsd2java() {
    def schemaVersion = '4.3'
    doLast {
        jaxbTargetDir.mkdirs()
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.jaxbTargetDir = jaxbTargetDir
        ant.xjc(
                destdir: '${jaxbTargetDir}',
                package: 'org.datacide.schema.kernel_4',
                schema: "https://schema.datacite.org/meta/kernel-${schemaVersion}/metadata.xsd"
        )
    }
}

tasks.withType(JavaCompile) {
    dependsOn(xsd2java)
}

tasks.withType(Checkstyle) {
    exclude { '**/generated/*' }
}

tasks.withType(Pmd) {
    exclude { '**/generated/*' }
}


build{
    dependsOn xsd2java
}

jar {
    from "$buildDir/classes/java/main"
}
